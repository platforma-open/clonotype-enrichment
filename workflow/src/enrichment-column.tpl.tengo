self := import("@platforma-sdk/workflow-tengo:tpl")
ll := import("@platforma-sdk/workflow-tengo:ll")
xsv := import("@platforma-sdk/workflow-tengo:pframes.xsv")
pSpec := import("@platforma-sdk/workflow-tengo:pframes.spec")
pframes := import("@platforma-sdk/workflow-tengo:pframes")
text := import("text")

pfEnrichmentConvExport := import(":pf-enrichment-conv-export")

self.defineOutputs("columnSpec", "columnData", "bindingSpecificitySpec", "bindingSpecificityData", "overallSpec", "overallData")

self.body(func(args) {
    min := args.min
    max := args.max
    cutoff := args.cutoff
    downsampling := args.downsampling
    abundanceSpec := args.abundanceSpec
    inputType := args.inputType
    topEnrichedColCsv := args.topEnrichedColCsv
    conditionOrder := args.conditionOrder
    controlConfig := args.controlConfig
    blockId := args.blockId
    specOpts := args.specOpts
    customBlockLabel := args.customBlockLabel
    defaultBlockLabel := args.defaultBlockLabel

    enrichmentImportParamsExport := undefined
    // In case of additional exports we will modify some spec values using specOpts
    if specOpts != undefined {
        enrichmentImportParamsExport = pfEnrichmentConvExport.getColumns(abundanceSpec, 
                                                                inputType,
                                                                string(min.getData()),
                                                                string(max.getData()),
                                                                string(cutoff.getData()),
                                                                downsampling,
                                                                conditionOrder,
                                                                controlConfig,
                                                                blockId,
                                                                specOpts)
    } else {
        enrichmentImportParamsExport = pfEnrichmentConvExport.getColumns(abundanceSpec, 
                                                                inputType,
                                                                string(min.getData()),
                                                                string(max.getData()),
                                                                string(cutoff.getData()),
                                                                downsampling,
                                                                conditionOrder,
                                                                controlConfig,
                                                                blockId)
    }

    exportFile := xsv.importFile(topEnrichedColCsv, 
                                "csv",
                                enrichmentImportParamsExport,
                                { cpu: 1, mem: "16GiB" })

    // Make trace with condition order (max enrichment will differ depending on the order)
    // Downsampling is also important to add to the label
	label := ""
	if downsampling.type == "hypergeometric" {
		if downsampling.valueChooser == "fixed" {
			label = "Downsampling: " + downsampling.type + " - " + downsampling.valueChooser + " - " + string(downsampling.n)
		} else {
			label = "Downsampling: " + downsampling.type + " - " + downsampling.valueChooser
		}
	} else {
		label = "Downsampling: " + downsampling.type
	}
    // Determine the label for condition order trace
    conditionOrderLabel := customBlockLabel || defaultBlockLabel

    // Add two traces, one for the condition order and one for the downsampling
    trace := pSpec.makeTrace(abundanceSpec,
        {
            type: "milaboratories.clonotype-enrichment.condition-order",
            importance: 35,
            label: conditionOrderLabel
        },
        {
            type: "milaboratories.clonotype-enrichment.downsampling",
            importance: 30,
            label: label
        })

    res := {
        columnSpec: trace.inject(exportFile["enrichment.spec"]),
        columnData: exportFile["enrichment.data"],
        overallSpec: trace.inject(exportFile["overall_log2fc.spec"]),
        overallData: exportFile["overall_log2fc.data"]
    }

    if controlConfig != undefined && controlConfig.enabled == true {
        res.bindingSpecificitySpec = trace.inject(exportFile["binding_specificity.spec"])
        res.bindingSpecificityData = exportFile["binding_specificity.data"]
    } else {
        res.bindingSpecificitySpec = "empty"
        res.bindingSpecificityData = "empty"
    }

    return res
})
